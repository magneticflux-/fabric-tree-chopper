plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.shipkit.java' version '2.3.1'
}

configurations {
    modShaded
    modImplementation.extendsFrom modShaded
}

repositories {
    maven {
        url = 'http://maven.fabricmc.net/'
        name = 'Fabric'
    }
    maven {
        url = 'https://minecraft.curseforge.com/api/maven'
        name = 'CurseForge'
    }
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

archivesBaseName = project.archives_base_name
group = project.maven_group

minecraft {
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:${project.kotlin_version}"
    include "net.fabricmc:fabric-language-kotlin:${project.kotlin_version}"

    modImplementation 'io.github.prospector:modmenu:1.12.2+build.17'

    modImplementation "me.shedaniel.cloth:config-2:${project.cloth_config_version}"
    include "me.shedaniel.cloth:config-2:${project.cloth_config_version}"

    modImplementation "me.shedaniel.cloth:fiber2cloth:${project.fiber_2_cloth_version}"
    include "me.shedaniel.cloth:fiber2cloth:${project.fiber_2_cloth_version}"

    modImplementation "me.zeroeightsix:fiber:${project.fiber_version}"
    include "me.zeroeightsix:fiber:${project.fiber_version}"
}

processResources {
    inputs.properties 'version': project.version,
            'archives_base_name': project.archives_base_name

    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'
        expand 'version': project.version,
                'archives_base_name': project.archives_base_name
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the 'build' task
// if it is present.
// If you remove this task, sources will not be generated.

// Tasks generated by ShipKit
/*
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from 'LICENSE'
}
*/

// Configure shadowing
shadowJar {
    configurations = [project.configurations.modShaded]
    //relocate 'some.library', 'my.mod.shadowed.some.library'
}

remapJar {
    dependsOn shadowJar
    input = shadowJar.archiveFile
}

// Remove the ShipKit publication and recreate it with Fabric artifacts
publishing.publications.getByName('javaLibrary') {
    delegate.artifacts = [remapJar, sourcesJar]
    delegate.getArtifacts() // Force evaluation here. Fails if lazily evaluated later for some reason...
}

// Configure CurseForge publishing
curseforge {
    // Stored in ~/.gradle/gradle.properties
    if (project.hasProperty('curseApiKey'))
        apiKey = project.curseApiKey
    else if (System.getenv("CURSE_API_KEY") != null)
        apiKey = System.getenv("CURSE_API_KEY")
    else
        println 'No CurseForge API key found, \'curseforge\' tasks will not work'

    project {
        id = project.curseforge_id
        releaseType = 'release'
        addGameVersion "$project.minecraft_version"
        addGameVersion 'Fabric'
        setChangelog file('docs/release-notes.md')
        mainArtifact(remapJar) {
            displayName = "$archives_base_name-$project.version"
            relations {
                requiredDependency 'fabric-api'
                embeddedLibrary 'fabric-language-kotlin'
                embeddedLibrary 'cloth-config'
                optionalDependency 'modmenu'
            }
        }
        addArtifact sourcesJar
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    // CurseGradle generates tasks in afterEvaluate for each project
    // There isn't really any other way to make it depend on a task unless it is an AbstractArchiveTask
    def curseforgeTask = tasks.getByName("curseforge${project.curseforge_id}")
    curseforgeTask.dependsOn publish
    curseforgeTask.dependsOn remapJar
    tasks.getByName('bintrayPublish').dependsOn curseforgeTask
}
